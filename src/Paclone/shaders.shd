//------------------------------------------------------------------------------
//  shaders.shd
//------------------------------------------------------------------------------

@vs canvasVS
in vec2 position;
in vec2 texcoord0;
out vec2 uv;
void main() {
    gl_Position = vec4(((position.xy * 2.0) - 1.0), 0.0, 1.0);
    gl_Position.y = -gl_Position.y;
    uv = texcoord0;
}
@end

@fs canvasFS
uniform sampler2D tex;
in vec2 uv;
out vec4 fragColor;
void main() {
    fragColor = texture(tex, uv);
}
@end

@program CanvasShader canvasVS canvasFS

//------------------------------------------------------------------------------
//  Timothy Lottes CRT effect shader:
//  https://www.shadertoy.com/view/XsjSzR#

@block TL_CRT
//
//
// PUBLIC DOMAIN CRT STYLED SCAN-LINE SHADER
//
//   by Timothy Lottes
//
// This is more along the style of a really good CGA arcade monitor.
// With RGB inputs instead of NTSC.
// The shadow mask example has the mask rotated 90 degrees for less chromatic aberration.
//
// Left it unoptimized to show the theory behind the algorithm.
//
// It is an example what I personally would want as a display option for pixel art games.
// Please take and use, change, or whatever.
//

// hardcoded to Pacman original display resolution
const vec2 res = vec2(224, 288);

// Hardness of scanline.
//  -8.0 = soft
// -16.0 = medium
const float hardScan=-8.0;

// Hardness of pixels in scanline.
// -2.0 = soft
// -4.0 = hard
const float hardPix=-2.0;

// Display warp.
// 0.0 = none
// 1.0/8.0 = extreme
//static const vec2 warp=vec2(1.0/32.0,1.0/32.0);
//static const vec2 warp=vec2(1.0/24.0,1.0/16.0);
const vec2 warp=vec2(1.0/32.0,1.0/24.0);

// Amount of shadow mask.
const float maskDark=0.75;
const float maskLight=2.0;

//------------------------------------------------------------------------

// sRGB to Linear.
// Assuing using sRGB typed textures this should not be needed.
float ToLinear1(float c){return(c<=0.04045)?c/12.92:pow((c+0.055)/1.055,2.4);}
vec3 ToLinear(vec3 c){return vec3(ToLinear1(c.r),ToLinear1(c.g),ToLinear1(c.b));}

// Linear to sRGB.
// Assuing using sRGB typed textures this should not be needed.
float ToSrgb1(float c){return(c<0.0031308?c*12.92:1.055*pow(c,0.41666)-0.055);}
vec3 ToSrgb(vec3 c){return vec3(ToSrgb1(c.r),ToSrgb1(c.g),ToSrgb1(c.b));}

// Nearest emulated sample given floating point position and texel offset.
// Also zero's off screen.
vec3 Fetch(sampler2D iChannel0, vec2 pos,vec2 off){
    pos=floor(pos*res+off)/res;
    if(max(abs(pos.x-0.5),abs(pos.y-0.5))>0.5)return vec3(0.0,0.0,0.0);
    return ToLinear(texture(iChannel0,pos.xy).rgb);}

// Distance in emulated pixels to nearest texel.
vec2 Dist(vec2 pos){pos=pos*res;return -((pos-floor(pos))-vec2(0.5,0.5));}

// 1D Gaussian.
float Gaus(float pos,float scale){return exp2(scale*pos*pos);}

// 3-tap Gaussian filter along horz line.
vec3 Horz3(sampler2D iChannel0, vec2 pos,float off){
    vec3 b=Fetch(iChannel0,pos,vec2(-1.0,off));
    vec3 c=Fetch(iChannel0,pos,vec2( 0.0,off));
    vec3 d=Fetch(iChannel0,pos,vec2( 1.0,off));
    float dst=Dist(pos).x;
    // Convert distance to weight.
    float scale=hardPix;
    float wb=Gaus(dst-1.0,scale);
    float wc=Gaus(dst+0.0,scale);
    float wd=Gaus(dst+1.0,scale);
    // Return filtered sample.
    return (b*wb+c*wc+d*wd)/(wb+wc+wd);}

// 5-tap Gaussian filter along horz line.
vec3 Horz5(sampler2D iChannel0, vec2 pos,float off){
    vec3 a=Fetch(iChannel0,pos,vec2(-2.0,off));
    vec3 b=Fetch(iChannel0,pos,vec2(-1.0,off));
    vec3 c=Fetch(iChannel0,pos,vec2( 0.0,off));
    vec3 d=Fetch(iChannel0,pos,vec2( 1.0,off));
    vec3 e=Fetch(iChannel0,pos,vec2( 2.0,off));
    float dst=Dist(pos).x;
    // Convert distance to weight.
    float scale=hardPix;
    float wa=Gaus(dst-2.0,scale);
    float wb=Gaus(dst-1.0,scale);
    float wc=Gaus(dst+0.0,scale);
    float wd=Gaus(dst+1.0,scale);
    float we=Gaus(dst+2.0,scale);
    // Return filtered sample.
    return (a*wa+b*wb+c*wc+d*wd+e*we)/(wa+wb+wc+wd+we);}

// Return scanline weight.
float Scan(vec2 pos,float off){
    float dst=Dist(pos).y;
    return Gaus(dst+off,hardScan);}

// Allow nearest three lines to effect pixel.
vec3 Tri(sampler2D iChannel0, vec2 pos){
    vec3 a=Horz3(iChannel0,pos,-1.0);
    vec3 b=Horz5(iChannel0,pos, 0.0);
    vec3 c=Horz3(iChannel0,pos, 1.0);
    float wa=Scan(pos,-1.0);
    float wb=Scan(pos, 0.0);
    float wc=Scan(pos, 1.0);
    return a*wa+b*wb+c*wc;}

// Distortion of scanlines, and end of screen alpha.
vec2 Warp(vec2 pos){
    pos=pos*2.0-1.0;
    pos*=vec2(1.0+(pos.y*pos.y)*warp.x,1.0+(pos.x*pos.x)*warp.y);
    return pos*0.5+0.5;}

// Shadow mask.
vec3 Mask(vec2 pos){
    pos.x+=pos.y*3.0;
    vec3 mask=vec3(maskDark,maskDark,maskDark);
    pos.x=fract(pos.x/6.0);
    if(pos.x<0.333)mask.r=maskLight;
    else if(pos.x<0.666)mask.g=maskLight;
    else mask.b=maskLight;
    return mask;}

vec4 tl_crt(sampler2D iChannel0, vec4 fragCoord, vec2 uv) {
    vec2 pos=Warp(uv);
    return vec4(ToSrgb(Tri(iChannel0, pos) * Mask(fragCoord.xy)), 1.0);}
@end

//------------------------------------------------------------------------------
@vs crtVS
in vec4 position;
in vec2 texcoord0;
out vec2 uv;
void main() {
    gl_Position = position;
    uv = texcoord0;
}
@end

@fs crtFS
@include TL_CRT
uniform sampler2D canvas;
in vec2 uv;
out vec4 fragColor;
void main() {
    fragColor = tl_crt(canvas, gl_FragCoord, uv);
}
@end

@fs nocrtFS
uniform sampler2D canvas;
in vec2 uv;
out vec4 fragColor;
void main() {
    fragColor = vec4(texture(canvas, uv).xyz, 1.0);
}
@end 

@program CRTShader crtVS crtFS
@program NoCRTShader crtVS nocrtFS

